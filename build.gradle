apply plugin: 'java'

wrapper {
    gradleVersion = "4.8.1"
}

repositories {
    mavenCentral()
}

sourceSets {
    unitTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/test/unit/java')
        }
        resources.srcDir file('src/test/unit/resources')
    }
    acceptanceTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/test/acceptance/java')
        }
        resources.srcDir file('src/test/acceptance/resources')
    }
}

configurations {
    unitTest.extendsFrom testCompile
    unitTest.extendsFrom testRuntime
    acceptanceTest.extendsFrom testCompile
    acceptanceTest.extendsFrom testRuntime
}

dependencies {
    testCompile 'org.junit.jupiter:junit-jupiter:5.4.0'
    testCompile 'org.assertj:assertj-core:3.12.1'
    acceptanceTestCompile 'org.concordion:concordion:2.2.0'
}

test {
    systemProperties['concordion.output.dir'] = "$reporting.baseDir/spec"
    testLogging.showStandardStreams = true
    outputs.upToDateWhen { false }
    useJUnitPlatform()
}

task acceptanceTest(type: Test) {
    testClassesDirs = sourceSets.acceptanceTest.output.classesDirs
    classpath = sourceSets.acceptanceTest.runtimeClasspath
}

task unitTest(type: Test) {
    testClassesDirs = sourceSets.unitTest.output.classesDirs
    classpath = sourceSets.unitTest.runtimeClasspath
}

check.dependsOn unitTest
check.dependsOn acceptanceTest
unitTest.mustRunAfter test
acceptanceTest.mustRunAfter unitTest